from: https://tools.ietf.org/html/rfc3986#section-2.1

La syntaxe d'URI générique recommande que les nouveaux modèles d'URI pour la représentation
des données de caractère dans un URI représentent, sans les traduire, les caractères de l'ensemble
non réservé, et convertissent tous les autres caractères en octets selon le codage UTF-8, pour
encoder ensuite ces valeurs en pourcent. Cette suggestion a été introduite en janvier 2005 avec
la publication de la RFC 39862. Les modèles d'URI introduits avant cette date ne sont pas concernés. 

URI producing applications should percent-encode data octets that
correspond to characters in the reserved set unless these characters
are specifically allowed by the URI scheme to represent data in that
component.  If a reserved character is found in a URI component and
no delimiting role is known for that character, then it must be
interpreted as representing the data octet corresponding to that
character's encoding in US-ASCII.

reserved    = gen-delims / sub-delims
gen-delims  = ":" / "/" / "?" / "#" / "[" / "]" / "@"
sub-delims  = "!" / "$" / "&" / "'" / "(" / ")"
                  / "*" / "+" / "," / ";" / "="
unreserved  = ALPHA / DIGIT / "-" / "." / "_" / "~"

URI         = scheme ":" hier-part [ "?" query ] [ "#" fragment ]
hier-part   = "//" authority path-abempty
             / path-absolute
             / path-rootless
             / path-empty

	foo://example.com:8042/over/there?name=ferret#nose
	\_/   \______________/\_________/ \_________/ \__/
	|           |            |            |        |
scheme     authority       path        query   fragment

authority percent-encode set	DEL:/?#[]@% "{}|\^[]`DEL
path percent-encode set			DEL/?#[]% "{}|\^[]`DEL
query percent-encode set		DEL#[]% "{}|\^[]`DEL


// SCHEME
scheme      = ALPHA *( ALPHA / DIGIT / "+" / "-" / "." )


// AUTHORITY
authority   = [ userinfo "@" ] host [ ":" port ]
userinfo    = *( unreserved / pct-encoded / sub-delims / ":" )
host        = IP-literal / IPv4address / reg-name
IP-literal = "[" ( IPv6address / IPvFuture  ) "]"
IPvFuture  = "v" 1*HEXDIG "." 1*( unreserved / sub-delims / ":" )
IPv4address = dec-octet "." dec-octet "." dec-octet "." dec-octet
dec-octet   = DIGIT                 ; 0-9
            / %x31-39 DIGIT         ; 10-99
            / "1" 2DIGIT            ; 100-199
            / "2" %x30-34 DIGIT     ; 200-249
            / "25" %x30-35          ; 250-255
reg-name    = *( unreserved / pct-encoded / sub-delims )
port        = *DIGIT


// PATH
path			= path-abempty    ; begins with "/" or is empty
				/ path-absolute   ; begins with "/" but not "//"
				/ path-noscheme   ; begins with a non-colon segment
				/ path-rootless   ; begins with a segment
				/ path-empty      ; zero characters

path-abempty  = *( "/" segment )
path-absolute = "/" [ segment-nz *( "/" segment ) ]
path-noscheme = segment-nz-nc *( "/" segment )
path-rootless = segment-nz *( "/" segment )
path-empty    = 0<pchar>
segment       = *pchar
segment-nz    = 1*pchar
segment-nz-nc = 1*( unreserved / pct-encoded / sub-delims / "@" ) ; non-zero-length segment without any colon ":"
pchar         = unreserved / pct-encoded / sub-delims / ":" / "@"

query       = *( pchar / "/" / "?" )
fragment    = *( pchar / "/" / "?" )